;license:MIT
;(c) 2025 by 4am
;
; drawing routines for Mono Upper 5 pixel font
;
; Public functions:
; - DrawString
;

HGRTops
         !byte $00, $08, $10, $18, $20, $28, $30, $38
         !byte $40, $48, $50, $58, $60, $68, $70, $78
         !byte $80, $88, $90, $98, $A0, $A8, $B0, $B8

;------------------------------------------------------------------------------
; DrawString
;
; in:    A/Y points to length-prefixed string (Pascal style, length byte can be 0)
;        X contains column number (0x00..0x27)
;        VTAB contains logical line number (0-indexed)
; out:   clobbers all registers & flags
;        clobbers HTAB
;        preserves VTAB
;------------------------------------------------------------------------------
DrawString
         +ST16 STRINGPTR
         stx   HTAB
         ldy   #0
         lda   (STRINGPTR), y
         bne   +
         rts
+        tax
         dex
         +LD16 STRINGPTR
         +INC16
         +ST16 @loop+1
         ldy   VTAB
         lda   HGRTops, y
         tay
         lda   HGRLO, y
         clc
         adc   HTAB
         sta   @row0+1
         lda   HGRHI, y
         sta   @row0+2
         iny
         lda   HGRLO, y
         adc   HTAB
         sta   @row1+1
         lda   HGRHI, y
         sta   @row1+2
         iny
         lda   HGRLO, y
         adc   HTAB
         sta   @row2+1
         lda   HGRHI, y
         sta   @row2+2
         iny
         lda   HGRLO, y
         adc   HTAB
         sta   @row3+1
         lda   HGRHI, y
         sta   @row3+2
         iny
         lda   HGRLO, y
         adc   HTAB
         sta   @row4+1
         lda   HGRHI, y
         sta   @row4+2
         iny
         lda   HGRLO, y
         adc   HTAB
         sta   @row5+1
         lda   HGRHI, y
         sta   @row5+2
         iny
         lda   HGRLO, y
         adc   HTAB
         sta   @row6+1
         lda   HGRHI, y
         sta   @row6+2
         iny
         lda   HGRLO, y
         adc   HTAB
         sta   @row7+1
         lda   HGRHI, y
         sta   @row7+2
@loop    ldy   $FDFD, x
         lda   FontDataRow0, y
@row0    sta   $FDFD, x
         lda   FontDataRow1, y
@row1    sta   $FDFD, x
         lda   FontDataRow2, y
@row2    sta   $FDFD, x
         lda   FontDataRow3, y
@row3    sta   $FDFD, x
         lda   FontDataRow4, y
@row4    sta   $FDFD, x
         lda   FontDataRow5, y
@row5    sta   $FDFD, x
         lda   FontDataRow6, y
@row6    sta   $FDFD, x
         lda   FontDataRow7, y
@row7    sta   $FDFD, x
         dex
         bpl   @loop
         rts
