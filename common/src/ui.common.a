;license:MIT
;(c) 2023 by 4am
;

;------------------------------------------------------------------------------
; Home
; clear graphics page 1
; don't laugh, this is 6x faster than calling HGR ($F3E2)
;
; in:    none
; out:   hi-res page 1 cleared and displayed
;        X preserved
;        A=0
;        Y=0
;        flags clobbered
;------------------------------------------------------------------------------
Home
         ldy   #$00
         sty   VTAB
         tya
-        sta   $2000, y
         sta   $2100, y
         sta   $2200, y
         sta   $2300, y
         sta   $2400, y
         sta   $2500, y
         sta   $2600, y
         sta   $2700, y
         sta   $2800, y
         sta   $2900, y
         sta   $2A00, y
         sta   $2B00, y
         sta   $2C00, y
         sta   $2D00, y
         sta   $2E00, y
         sta   $2F00, y
         sta   $3000, y
         sta   $3100, y
         sta   $3200, y
         sta   $3300, y
         sta   $3400, y
         sta   $3500, y
         sta   $3600, y
         sta   $3700, y
         sta   $3800, y
         sta   $3900, y
         sta   $3A00, y
         sta   $3B00, y
         sta   $3C00, y
         sta   $3D00, y
         sta   $3E00, y
         sta   $3F00, y
         iny
         bne   -
         bit   PAGE1
         bit   FULLGFX
         bit   HIRES
         bit   GFXMODE
         rts
