;license:MIT
;(c) 2023 by 4am
;
; drawing routines for Mono Number 25 pixel font
;
; Public functions:
; - DrawString
;

HGRTops
         !byte 0, 6, 12, 18, 24, 30, 36, 42, 48, 54, 60
         !byte 66, 72, 78, 84, 90, 96, 102, 108, 114, 120
         !byte 126, 132, 138, 144, 150, 156, 162, 168, 174, 180
         !byte 186

;------------------------------------------------------------------------------
; DrawString
;
; in:    A/Y points to length-prefixed string (Pascal style, length byte can be 0)
;        X contains column number (0x00..0x27)
;        VTAB contains logical line number (0-indexed)
; out:   clobbers all registers & flags
;        clobbers HTAB
;        preserves VTAB
;------------------------------------------------------------------------------
DrawString
         +ST16 STRINGPTR
         stx   HTAB
         ldy   #0
         lda   (STRINGPTR), y
         bne   +
         rts
+        tax
         dex
         +LD16 STRINGPTR
         +INC16
         +ST16 @loop+1
         ldy   VTAB
         lda   HGRTops, y
         tay
         lda   HGRLO, y
         clc
         adc   HTAB
         sta   @row0+1
         lda   HGRHI, y
         sta   @row0+2
         iny
         lda   HGRLO, y
         adc   HTAB
         sta   @row1+1
         lda   HGRHI, y
         sta   @row1+2
         iny
         lda   HGRLO, y
         adc   HTAB
         sta   @row2+1
         lda   HGRHI, y
         sta   @row2+2
         iny
         lda   HGRLO, y
         adc   HTAB
         sta   @row3+1
         lda   HGRHI, y
         sta   @row3+2
         iny
         lda   HGRLO, y
         adc   HTAB
         sta   @row4+1
         lda   HGRHI, y
         sta   @row4+2
@loop    ldy   $FDFD, x
         lda   FontDataRow0-kFontDataMin, y
@row0    sta   $FDFD, x
         lda   FontDataRow1-kFontDataMin, y
@row1    sta   $FDFD, x
         lda   FontDataRow2-kFontDataMin, y
@row2    sta   $FDFD, x
         lda   FontDataRow3-kFontDataMin, y
@row3    sta   $FDFD, x
         lda   FontDataRow4-kFontDataMin, y
@row4    sta   $FDFD, x
         dex
         bpl   @loop
         rts
